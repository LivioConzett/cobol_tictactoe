000001 IDENTIFICATION DIVISION.
000002 PROGRAM-ID. TICTACTOE.
000003*---------------------------------------------------
000004* TIC TAC TOE GAME WITH COMPUTER OPPONENT
000005*---------------------------------------------------
000006 ENVIRONMENT DIVISION.
000007 INPUT-OUTPUT SECTION.
000008 FILE-CONTROL.
000009 
000010     SELECT TITLE-SCREEN-FILE
000011         ASSIGN TO "./data/TITLE"
000012         ORGANIZATION IS SEQUENTIAL.
000013     
000014     SELECT X-FILE
000015         ASSIGN TO "./data/X"
000016         ORGANIZATION IS SEQUENTIAL.
000017
000018     SELECT O-FILE
000019         ASSIGN TO "./data/O"
000020         ORGANIZATION IS SEQUENTIAL.
000021
000022 DATA DIVISION.
000023 FILE SECTION.
000024
000025 FD  TITLE-SCREEN-FILE LABEL RECORDS ARE STANDARD.
000026 01  TITLE-SCREEN-RECORD                PIC X(80).
000027
000028 FD  X-FILE LABEL RECORDS ARE STANDARD.
000029 01  X-FILE-RECORD                      PIC X(12).
000030 
000031 FD  O-FILE LABEL RECORDS ARE STANDARD.
000032 01  O-FILE-RECORD                      PIC X(12).
000033 
000034 WORKING-STORAGE SECTION.
000035
000036* -----------------------------------------------------
000037*        COLORS 
000038* -----------------------------------------------------
000039 01  BLACK                       CONSTANT AS 0.
000040 01  BLUE                        CONSTANT AS 1.
000041 01  GREEN                       CONSTANT AS 2.
000042 01  CYAN                        CONSTANT AS 3.
000043 01  RED                         CONSTANT AS 4.
000044 01  MAGENTA                     CONSTANT AS 5.
000045 01  BROWN                       CONSTANT AS 6.
000046 01  WHITE                       CONSTANT AS 7.
000047 
000048 77  DEBUG-TEXT                  PIC X(80).
000049 77  A-DUMMY                     PIC X.
000050 77  ERROR-MESSAGE               PIC X(80).
000051 77  GAME-OVER                   PIC X.
000052 77  MOVES-MADE                  PIC 9.
000053 77  COMP-MOVE-MADE              PIC X.
000054 
000055 77  AMOUNT-OF-PLAYERS-INPUT     PIC X(3).
000056 77  AMOUNT-OF-PLAYERS           PIC 9.
000057     88 AMOUNT-OF-PLAYERS-VALID  VALUES 1, 2.
000058 
000059 77  COMP-LEVEL-INPUT            PIC X(3).
000060 77  COMP-LEVEL                  PIC 9.
000061     88 COMP-LEVEL-VALID         VALUES 1 THRU 6.
000062 
000063 77  INPUT-PLAY-AGAIN            PIC X(4).
000064 77  PLAY-AGAIN                  PIC X.
000065     88  PLAY-AGAIN-YES          VALUES "Y", "y".
000066
000067
000068 77  FIELD-CALCULATION           PIC 99 VALUE ZEROES.
000069 77  CHECK-INDEX                 PIC 99.
000070 77  CHECK-SUM                   PIC 99.
000071 77  END-MESSAGE                 PIC X(13).
000072 77  END-MESSAGE-COLOR           PIC 9 VALUE 3.
000073
000074 77  INPUT-PLAYER-MOVE           PIC X(4).
000075 77  PLAYER-MOVE                 PIC 9.
000076     88  PLAYER-MOVE-NUM         VALUES 1 THRU 9.
000077 77  PLAYER-MOVE-VALID           PIC X VALUE "N".
000078
000079* -----------------------------------------------------
000080*        TITLE SCREEN
000081* -----------------------------------------------------
000082 77  FILE-AT-END                 PIC X.
000083
000084 01  TITLE.
000085     05  TITLE-LINE              OCCURS 24 TIMES.
000086         10  TITLE-LINE-TEXT         PIC X(80).
000087
000088 77  LINE-INDEX                  PIC 99 VALUE ZEROS.
000089 
000090* -----------------------------------------------------
000091*        X'S AND O'S
000092* -----------------------------------------------------
000093 01  X-PIECE.
000094     05  X-PIECE-LINE            OCCURS 6 TIMES.
000095         10 X-PIECE-LINE-TEXT    PIC X(12).
000096
000097 01  O-PIECE.
000098     05  O-PIECE-LINE            OCCURS 6 TIMES.
000099         10 O-PIECE-LINE-TEXT    PIC X(12).
000100
000101* -----------------------------------------------------
000102*        FIELD LOGIC
000103* -----------------------------------------------------
000104 01  PLAYING-FIELD-LOGIC.
000105     05  FIELD-SQUARE            PIC 9 OCCURS 9 TIMES.
000106*        10  FIELD-SQUARE-TEXT   PIC 9.
000107 
000108 01  PLAYING-FIELD-DISPLAY.
000109     05  FIELD-SQUARE-DISPLAY    OCCURS 9 TIMES.
000110         10  FIELD-DISPLAY-TEXT  PIC X(11) OCCURS 6 TIMES.
000111
000112 01  PLAYING-FIELD-COLORS.
000113     05  FIELD-DISPLAY-COLORS    OCCURS 9 TIMES.
000114         10  FIELD-DISPLAY-COLOR PIC 9.
000115 
000116 01  PLAYING-FIELD-DISPLAY-COLORS.
000117     05  COLOR-1                 PIC 9.
000118     05  COLOR-2                 PIC 9.
000119     05  COLOR-3                 PIC 9.
000120     05  COLOR-4                 PIC 9.
000121     05  COLOR-5                 PIC 9.
000122     05  COLOR-6                 PIC 9.
000123     05  COLOR-7                 PIC 9.
000124     05  COLOR-8                 PIC 9.
000125     05  COLOR-9                 PIC 9.
000126 
000127 01  WIN-COLOR                   PIC 9.
000128 
000129 01  PLAYING-FIELD-NUMBER        PIC 9.
000130 01  PLAYER-NAME                 PIC X(8).
000131
000132 77  DEBUG                       PIC X(80) VALUE SPACES.
000133
000134 SCREEN SECTION.
000135 
000136 01  HOME-SCREEN.
000137     05  BLANK SCREEN.
000138     05  LINE  1 COL  1 PIC X(80) FROM TITLE-LINE(1).
000139     05  LINE  2 COL  1 PIC X(80) FROM TITLE-LINE(2).
000140     05  LINE  3 COL  1 PIC X(80) FROM TITLE-LINE(3).
000141     05  LINE  4 COL  1 PIC X(80) FROM TITLE-LINE(4).
000142     05  LINE  5 COL  1 PIC X(80) FROM TITLE-LINE(5).
000143     05  LINE  6 COL  1 PIC X(80) FROM TITLE-LINE(6).
000144     05  LINE  7 COL  1 PIC X(80) FROM TITLE-LINE(7).
000145     05  LINE  8 COL  1 PIC X(80) FROM TITLE-LINE(8).
000146     05  LINE  9 COL  1 PIC X(80) FROM TITLE-LINE(9).
000147     05  LINE 10 COL  1 PIC X(80) FROM TITLE-LINE(10).
000148     05  LINE 11 COL  1 PIC X(80) FROM TITLE-LINE(11).
000149     05  LINE 12 COL  1 PIC X(80) FROM TITLE-LINE(12).
000150     05  LINE 13 COL  1 PIC X(80) FROM TITLE-LINE(13).
000151     05  LINE 14 COL  1 PIC X(80) FROM TITLE-LINE(14).
000152     05  LINE 15 COL  1 PIC X(80) FROM TITLE-LINE(15).
000153     05  LINE 16 COL  1 PIC X(80) FROM TITLE-LINE(16).
000154     05  LINE 17 COL  1 PIC X(80) FROM TITLE-LINE(17).
000155     05  LINE 18 COL  1 PIC X(80) FROM TITLE-LINE(18).
000156     05  LINE 19 COL  1 PIC X(80) FROM TITLE-LINE(19).
000157     05  LINE 20 COL  1 PIC X(80) FROM TITLE-LINE(20).
000158     05  LINE 21 COL  1 PIC X(80) FROM TITLE-LINE(21).
000159     05  LINE 22 COL  1 PIC X(80) FROM TITLE-LINE(22).
000160     05  LINE 23 COL  1 PIC X(80) FROM TITLE-LINE(23).
000161     05  LINE 24 COL  1 PIC X(80) FROM TITLE-LINE(24).
000162     05  LINE 25 COL  1 PIC Z USING A-DUMMY.
000163
000164 01  SETTING-SCREEN.
000165     05  BLANK SCREEN.
000166     05  LINE  4 COL  20 PIC X(24)
000167         VALUE "How many Players (1/2)?".
000168     05  LINE  4 COL  44 PIC X(3) USING AMOUNT-OF-PLAYERS-INPUT.
000169     05  LINE  6 COL  20 PIC X(16)
000170         VALUE "CPU LEVEL (1-6)?".
000171     05  LINE  6 COL  37 PIC X(3) USING COMP-LEVEL-INPUT.
000172     05  LINE 24 COL  1 PIC X(80) FROM ERROR-MESSAGE
000173             FOREGROUND-COLOR RED.
000174 
000175 01  PLAYING-FIELD.
000176     05  BLANK SCREEN.
000177* VERTICAL LINE ONE
000178     05  LINE  1 COL  1 PIC X(80) FROM DEBUG.
000179     05  LINE  2 COL 26 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000180     05  LINE  3 COL 26 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000181     05  LINE  4 COL 26 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000182     05  LINE  5 COL 26 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000183     05  LINE  6 COL 26 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000184     05  LINE  7 COL 26 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000185     05  LINE  8 COL 26 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000186     05  LINE  9 COL 26 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000187     05  LINE 10 COL 26 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000188     05  LINE 11 COL 26 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000189     05  LINE 12 COL 26 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000190     05  LINE 13 COL 26 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000191     05  LINE 14 COL 26 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000192     05  LINE 15 COL 26 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000193     05  LINE 16 COL 26 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000194     05  LINE 17 COL 26 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000195     05  LINE 17 COL 26 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000196     05  LINE 18 COL 26 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000197     05  LINE 19 COL 26 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000198     05  LINE 20 COL 26 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000199     05  LINE 21 COL 26 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000200     05  LINE 22 COL 26 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000201
000202* VERTICAL LINE TWO
000203     05  LINE  2 COL 42 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000204     05  LINE  3 COL 42 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000205     05  LINE  4 COL 42 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000206     05  LINE  5 COL 42 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000207     05  LINE  6 COL 42 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000208     05  LINE  7 COL 42 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000209     05  LINE  8 COL 42 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000210     05  LINE  9 COL 42 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000211     05  LINE 10 COL 42 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000212     05  LINE 11 COL 42 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000213     05  LINE 12 COL 42 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000214     05  LINE 13 COL 42 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000215     05  LINE 14 COL 42 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000216     05  LINE 15 COL 42 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000217     05  LINE 16 COL 42 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000218     05  LINE 17 COL 42 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000219     05  LINE 17 COL 42 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000220     05  LINE 18 COL 42 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000221     05  LINE 19 COL 42 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000222     05  LINE 20 COL 42 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000223     05  LINE 21 COL 42 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000224     05  LINE 22 COL 42 PIC XX VALUE "  " BACKGROUND-COLOR WHITE.
000225
000226*  HORIZONTAL LINE ONE 
000227     05  LINE 8 COL 12 PIC X(14) VALUE 
000228             "--------------"
000229             BACKGROUND-COLOR WHITE.
000230     05  LINE 8 COL 28 PIC X(14) VALUE 
000231             "--------------"
000232             BACKGROUND-COLOR WHITE.
000233     05  LINE 8 COL 44 PIC X(14) VALUE 
000234             "--------------"
000235             BACKGROUND-COLOR WHITE.
000236
000237*  HORIZONTAL LINE TWO 
000238     05  LINE 16 COL 12 PIC X(14) VALUE 
000239             "--------------"
000240             BACKGROUND-COLOR WHITE.
000241     05  LINE 16 COL 28 PIC X(14) VALUE 
000242             "--------------"
000243             BACKGROUND-COLOR WHITE.
000244     05  LINE 16 COL 44 PIC X(14) VALUE 
000245             "--------------"
000246             BACKGROUND-COLOR WHITE.
000247
000248*  FIELD NUMBERS 
000249     05  LINE  7 COL 18 PIC 9 VALUE 1.
000250     05  LINE  7 COL 35 PIC 9 VALUE 2.
000251     05  LINE  7 COL 50 PIC 9 VALUE 3.
000252     05  LINE 15 COL 18 PIC 9 VALUE 4.
000253     05  LINE 15 COL 35 PIC 9 VALUE 5.
000254     05  LINE 15 COL 50 PIC 9 VALUE 6.
000255     05  LINE 23 COL 18 PIC 9 VALUE 7.
000256     05  LINE 23 COL 35 PIC 9 VALUE 8.
000257     05  LINE 23 COL 50 PIC 9 VALUE 9.
000258
000259*  FIELD VALUES
000260*    05  LINE  4 COL 18 PIC 9 FROM FIELD-SQUARE(1).
000261*    05  LINE  4 COL 35 PIC 9 FROM FIELD-SQUARE(2).
000262*    05  LINE  4 COL 50 PIC 9 FROM FIELD-SQUARE(3).
000263*    05  LINE 12 COL 18 PIC 9 FROM FIELD-SQUARE(4).
000264*    05  LINE 12 COL 35 PIC 9 FROM FIELD-SQUARE(5).
000265*    05  LINE 12 COL 50 PIC 9 FROM FIELD-SQUARE(6).
000266*    05  LINE 20 COL 18 PIC 9 FROM FIELD-SQUARE(7).
000267*    05  LINE 20 COL 35 PIC 9 FROM FIELD-SQUARE(8).
000268*    05  LINE 20 COL 50 PIC 9 FROM FIELD-SQUARE(9).
000269
000270*  FIELD DISPLAY 
000271* 1
000272     05  LINE  2 COL 14 PIC X(10) FROM FIELD-DISPLAY-TEXT(1,1)
000273         FOREGROUND-COLOR COLOR-1.
000274     05  LINE  3 COL 14 PIC X(10) FROM FIELD-DISPLAY-TEXT(1,2)
000275         FOREGROUND-COLOR COLOR-1.
000276     05  LINE  4 COL 14 PIC X(10) FROM FIELD-DISPLAY-TEXT(1,3)
000277         FOREGROUND-COLOR COLOR-1.
000278     05  LINE  5 COL 14 PIC X(10) FROM FIELD-DISPLAY-TEXT(1,4)
000279         FOREGROUND-COLOR COLOR-1.
000280     05  LINE  6 COL 14 PIC X(10) FROM FIELD-DISPLAY-TEXT(1,5)
000281         FOREGROUND-COLOR COLOR-1.
000282* 2
000283     05  LINE  2 COL 30 PIC X(10) FROM FIELD-DISPLAY-TEXT(2,1)
000284         FOREGROUND-COLOR COLOR-2.
000285     05  LINE  3 COL 30 PIC X(10) FROM FIELD-DISPLAY-TEXT(2,2)
000286         FOREGROUND-COLOR COLOR-2.
000287     05  LINE  4 COL 30 PIC X(10) FROM FIELD-DISPLAY-TEXT(2,3)
000288         FOREGROUND-COLOR COLOR-2.
000289     05  LINE  5 COL 30 PIC X(10) FROM FIELD-DISPLAY-TEXT(2,4)
000290         FOREGROUND-COLOR COLOR-2.
000291     05  LINE  6 COL 30 PIC X(10) FROM FIELD-DISPLAY-TEXT(2,5)
000292         FOREGROUND-COLOR COLOR-2.
000293* 3
000294     05  LINE  2 COL 46 PIC X(10) FROM FIELD-DISPLAY-TEXT(3,1)
000295         FOREGROUND-COLOR COLOR-3.
000296     05  LINE  3 COL 46 PIC X(10) FROM FIELD-DISPLAY-TEXT(3,2)
000297         FOREGROUND-COLOR COLOR-3.
000298     05  LINE  4 COL 46 PIC X(10) FROM FIELD-DISPLAY-TEXT(3,3)
000299         FOREGROUND-COLOR COLOR-3.
000300     05  LINE  5 COL 46 PIC X(10) FROM FIELD-DISPLAY-TEXT(3,4)
000301         FOREGROUND-COLOR COLOR-3.
000302     05  LINE  6 COL 46 PIC X(10) FROM FIELD-DISPLAY-TEXT(3,5)
000303         FOREGROUND-COLOR COLOR-3.
000304
000305* 4
000306     05  LINE 10 COL 14 PIC X(10) FROM FIELD-DISPLAY-TEXT(4,1)
000307         FOREGROUND-COLOR COLOR-4.
000308     05  LINE 11 COL 14 PIC X(10) FROM FIELD-DISPLAY-TEXT(4,2)
000309         FOREGROUND-COLOR COLOR-4.
000310     05  LINE 12 COL 14 PIC X(10) FROM FIELD-DISPLAY-TEXT(4,3)
000311         FOREGROUND-COLOR COLOR-4.
000312     05  LINE 13 COL 14 PIC X(10) FROM FIELD-DISPLAY-TEXT(4,4)
000313         FOREGROUND-COLOR COLOR-4.
000314     05  LINE 14 COL 14 PIC X(10) FROM FIELD-DISPLAY-TEXT(4,5)
000315         FOREGROUND-COLOR COLOR-4.
000316* 5
000317     05  LINE 10 COL 30 PIC X(10) FROM FIELD-DISPLAY-TEXT(5,1)
000318         FOREGROUND-COLOR COLOR-5.
000319     05  LINE 11 COL 30 PIC X(10) FROM FIELD-DISPLAY-TEXT(5,2)
000320         FOREGROUND-COLOR COLOR-5.
000321     05  LINE 12 COL 30 PIC X(10) FROM FIELD-DISPLAY-TEXT(5,3)
000322         FOREGROUND-COLOR COLOR-5.
000323     05  LINE 13 COL 30 PIC X(10) FROM FIELD-DISPLAY-TEXT(5,4)
000324         FOREGROUND-COLOR COLOR-5.
000325     05  LINE 14 COL 30 PIC X(10) FROM FIELD-DISPLAY-TEXT(5,5)
000326         FOREGROUND-COLOR COLOR-5.
000327* 6
000328     05  LINE 10 COL 46 PIC X(10) FROM FIELD-DISPLAY-TEXT(6,1)
000329         FOREGROUND-COLOR COLOR-6.
000330     05  LINE 11 COL 46 PIC X(10) FROM FIELD-DISPLAY-TEXT(6,2)
000331         FOREGROUND-COLOR COLOR-6.
000332     05  LINE 12 COL 46 PIC X(10) FROM FIELD-DISPLAY-TEXT(6,3)
000333         FOREGROUND-COLOR COLOR-6.
000334     05  LINE 13 COL 46 PIC X(10) FROM FIELD-DISPLAY-TEXT(6,4)
000335         FOREGROUND-COLOR COLOR-6.
000336     05  LINE 14 COL 46 PIC X(10) FROM FIELD-DISPLAY-TEXT(6,5)
000337         FOREGROUND-COLOR COLOR-6.
000338
000339* 7
000340     05  LINE 18 COL 14 PIC X(10) FROM FIELD-DISPLAY-TEXT(7,1)
000341         FOREGROUND-COLOR COLOR-7.
000342     05  LINE 19 COL 14 PIC X(10) FROM FIELD-DISPLAY-TEXT(7,2)
000343         FOREGROUND-COLOR COLOR-7.
000344     05  LINE 20 COL 14 PIC X(10) FROM FIELD-DISPLAY-TEXT(7,3)
000345         FOREGROUND-COLOR COLOR-7.
000346     05  LINE 21 COL 14 PIC X(10) FROM FIELD-DISPLAY-TEXT(7,4)
000347         FOREGROUND-COLOR COLOR-7.
000348     05  LINE 22 COL 14 PIC X(10) FROM FIELD-DISPLAY-TEXT(7,5)
000349         FOREGROUND-COLOR COLOR-7.
000350* 8
000351     05  LINE 18 COL 30 PIC X(10) FROM FIELD-DISPLAY-TEXT(8,1)
000352         FOREGROUND-COLOR COLOR-8.
000353     05  LINE 19 COL 30 PIC X(10) FROM FIELD-DISPLAY-TEXT(8,2)
000354         FOREGROUND-COLOR COLOR-8.
000355     05  LINE 20 COL 30 PIC X(10) FROM FIELD-DISPLAY-TEXT(8,3)
000356         FOREGROUND-COLOR COLOR-8.
000357     05  LINE 21 COL 30 PIC X(10) FROM FIELD-DISPLAY-TEXT(8,4)
000358         FOREGROUND-COLOR COLOR-8.
000359     05  LINE 22 COL 30 PIC X(10) FROM FIELD-DISPLAY-TEXT(8,5)
000360         FOREGROUND-COLOR COLOR-8.
000361* 9
000362     05  LINE 18 COL 46 PIC X(10) FROM FIELD-DISPLAY-TEXT(9,1)
000363         FOREGROUND-COLOR COLOR-9.
000364     05  LINE 19 COL 46 PIC X(10) FROM FIELD-DISPLAY-TEXT(9,2)
000365         FOREGROUND-COLOR COLOR-9.
000366     05  LINE 20 COL 46 PIC X(10) FROM FIELD-DISPLAY-TEXT(9,3)
000367         FOREGROUND-COLOR COLOR-9.
000368     05  LINE 21 COL 46 PIC X(10) FROM FIELD-DISPLAY-TEXT(9,4)
000369         FOREGROUND-COLOR COLOR-9.
000370     05  LINE 22 COL 46 PIC X(10) FROM FIELD-DISPLAY-TEXT(9,5)
000371         FOREGROUND-COLOR COLOR-9.
000372
000373
000374*  ERROR MESSAGE SPACE AND INPUT
000375     05  LINE 24 COL  1 PIC X(80) FROM ERROR-MESSAGE
000376             FOREGROUND-COLOR RED.
000377     05  LINE 25 COL  1 PIC X(8) FROM PLAYER-NAME.
000378     05  LINE 25 COL 10 PIC X(11) VALUE "MOVE (1-9):".
000379     05  LINE 25 COL 22 PIC X(4) USING INPUT-PLAYER-MOVE.
000380
000381 01  END-SCREEN-SCREEN.
000382     05  LINE 10 COL 22 PIC X(26) VALUE
000383         "                          "
000384         BACKGROUND-COLOR END-MESSAGE-COLOR.
000385
000386     05  LINE 11 COL 22 PIC X(26) VALUE
000387         "                          "
000388         BACKGROUND-COLOR END-MESSAGE-COLOR.
000389
000390     05  LINE 11 COL 25 PIC X(13) FROM END-MESSAGE
000391         FOREGROUND-COLOR BLACK
000392         BACKGROUND-COLOR END-MESSAGE-COLOR.
000393
000394     05  LINE 12 COL 22 PIC X(26) VALUE
000395         "                          "
000396         BACKGROUND-COLOR END-MESSAGE-COLOR.
000397
000398*    05  LINE 13 COL 20 PIC X(31) VALUE
000399*        "                               "
000400*        BACKGROUND-COLOR END-MESSAGE-COLOR.
000401
000402     05  LINE 12 COL 25 PIC X(12) VALUE "AGAIN (Y/N)?"
000403         FOREGROUND-COLOR BLACK
000404         BACKGROUND-COLOR END-MESSAGE-COLOR.
000405
000406     05  LINE 12 COL 38 PIC X(4) USING INPUT-PLAY-AGAIN 
000407         FOREGROUND-COLOR BLACK
000408         BACKGROUND-COLOR END-MESSAGE-COLOR.
000409
000410     05  LINE 13 COL 22 PIC X(26) VALUE
000411         "                          "
000412         BACKGROUND-COLOR END-MESSAGE-COLOR.
000413
000414*01  DEBUG.
000415*    05  LINE 2 COL 1 PIC X(80) FROM DEBUG-TEXT
000416*        BACKGROUND-COLOR BLACK FOREGROUND-COLOR WHITE.
000417
000418 PROCEDURE DIVISION.
000419 PROGRAM-BEGIN.
000420     PERFORM OPENING-PROCEDURE.
000421     PERFORM MAIN-PROCESS.
000422     PERFORM CLOSING-PROCEDURE.
000423
000424 PROGRAM-EXIT.
000425     EXIT PROGRAM.
000426
000427 PROGRAM-DONE.
000428     STOP RUN.
000429
000430 OPENING-PROCEDURE.
000431     PERFORM GET-TITLE-SCREEN.
000432     PERFORM GET-X.
000433     PERFORM GET-O.
000434*    PERFORM DEBUG-PIECES.
000435     DISPLAY HOME-SCREEN.
000436     ACCEPT HOME-SCREEN.
000437     
000438     PERFORM GET-SETTINGS.
000439     
000440
000441 CLOSING-PROCEDURE.
000442     
000443
000444 MAIN-PROCESS.
000445     PERFORM INIT-THE-GAME.
000446     PERFORM PLAY-GAME
000447         UNTIL GAME-OVER = "Y"
000448            OR MOVES-MADE >= 9.
000449     PERFORM END-SCREEN.
000450
000451* TOOL TO DEBUG THE SIZE AND SHAP OF THE PIECES.
000452*DEBUG-PIECES.
000453*    MOVE O-PIECE TO FIELD-SQUARE-DISPLAY(1).
000454
000455* -----------------------------------------------------
000456*        GET FILES 
000457* -----------------------------------------------------
000458 GET-TITLE-SCREEN.
000459     MOVE ZEROES TO LINE-INDEX.
000460     MOVE SPACES TO TITLE.
000461     MOVE SPACES TO PLAYING-FIELD-DISPLAY.
000462     MOVE "N" TO FILE-AT-END.
000463     OPEN INPUT TITLE-SCREEN-FILE.
000464     PERFORM READ-IN-TITLE-SCREEN
000465         UNTIL FILE-AT-END = "Y".
000466     CLOSE TITLE-SCREEN-FILE.
000467*    DISPLAY TITLE.
000468
000469
000470 READ-IN-TITLE-SCREEN.
000471     ADD 1 TO LINE-INDEX.
000472     READ TITLE-SCREEN-FILE NEXT RECORD 
000473         AT END 
000474             MOVE "Y" TO FILE-AT-END.
000475     MOVE TITLE-SCREEN-RECORD TO TITLE-LINE(LINE-INDEX).
000476
000477 GET-X.
000478     MOVE ZEROES TO LINE-INDEX.
000479     MOVE SPACES TO X-PIECE.
000480     MOVE "N" TO FILE-AT-END.
000481     OPEN INPUT X-FILE.
000482     PERFORM READ-IN-X-FILE
000483         UNTIL FILE-AT-END = "Y".
000484     CLOSE X-FILE.
000485
000486 READ-IN-X-FILE.
000487     ADD 1 TO LINE-INDEX.
000488     READ X-FILE NEXT RECORD 
000489         AT END 
000490             MOVE "Y" TO FILE-AT-END.
000491     MOVE X-FILE-RECORD TO X-PIECE-LINE(LINE-INDEX).
000492
000493 GET-O.
000494     MOVE ZEROES TO LINE-INDEX.
000495     MOVE SPACES TO O-PIECE.
000496     MOVE "N" TO FILE-AT-END.
000497     OPEN INPUT O-FILE.
000498     PERFORM READ-IN-O-FILE
000499         UNTIL FILE-AT-END = "Y".
000500     CLOSE O-FILE.
000501
000502 READ-IN-O-FILE.
000503     ADD 1 TO LINE-INDEX.
000504     READ O-FILE NEXT RECORD 
000505         AT END 
000506             MOVE "Y" TO FILE-AT-END.
000507     MOVE O-FILE-RECORD TO O-PIECE-LINE(LINE-INDEX).
000508
000509* -----------------------------------------------------
000510*        GET SETTINGS
000511* -----------------------------------------------------
000512 GET-SETTINGS.
000513     PERFORM ASK-PLAYER-FOR-SETTINGS.
000514
000515* ONLY CHECK IF CPU LEVEL IS VALID IF PLAYER AMOUNT IS 1
000516     PERFORM RE-ASK-PLAYER-FOR-SETTINGS
000517         UNTIL AMOUNT-OF-PLAYERS-VALID
000518             AND (
000519               (COMP-LEVEL-VALID AND AMOUNT-OF-PLAYERS = 1)
000520               OR 
000521               (AMOUNT-OF-PLAYERS = 2)
000522               ).
000523*    MOVE COMP-LEVEL TO DEBUG.
000524
000525 ASK-PLAYER-FOR-SETTINGS.
000526     DISPLAY SETTING-SCREEN.
000527     ACCEPT SETTING-SCREEN.
000528     MOVE SPACES TO ERROR-MESSAGE.
000529     MOVE AMOUNT-OF-PLAYERS-INPUT TO AMOUNT-OF-PLAYERS.
000530     MOVE COMP-LEVEL-INPUT TO COMP-LEVEL.
000531 
000532 RE-ASK-PLAYER-FOR-SETTINGS.
000533     MOVE "INPUT NOT CORRECT - PLEASE TRY AGAIN."
000534          TO ERROR-MESSAGE.
000535     PERFORM ASK-PLAYER-FOR-SETTINGS.
000536
000537* -----------------------------------------------------
000538*        INITIALIZATION STUFF
000539* -----------------------------------------------------
000540 INIT-THE-GAME.
000541     MOVE ZEROES TO PLAYING-FIELD-LOGIC.
000542     MOVE SPACES TO PLAYING-FIELD-DISPLAY.
000543     MOVE SPACES TO ERROR-MESSAGE.
000544     MOVE "IT'S A TIE!" TO END-MESSAGE.
000545     MOVE CYAN TO END-MESSAGE-COLOR.
000546     MOVE "N" TO GAME-OVER.
000547     MOVE ZEROES TO MOVES-MADE.
000548     MOVE ZEROES TO PLAYER-MOVE.
000549     PERFORM INIT-FIELD-COLORS.
000550
000551 INIT-FIELD-COLORS.
000552     MOVE WHITE TO COLOR-1
000553                   COLOR-2
000554                   COLOR-3
000555                   COLOR-4
000556                   COLOR-5
000557                   COLOR-6
000558                   COLOR-7
000559                   COLOR-8
000560                   COLOR-9.
000561     
000562     PERFORM INIT-COLOR-ARRAY
000563         VARYING CHECK-INDEX FROM 1 BY 1
000564         UNTIL CHECK-INDEX > 9.
000565
000566 INIT-COLOR-ARRAY.
000567     MOVE WHITE TO FIELD-DISPLAY-COLOR(CHECK-INDEX).
000568
000569* -----------------------------------------------------
000570*        GAME LOOP
000571* -----------------------------------------------------
000572 PLAY-GAME.
000573     MOVE 7 TO PLAYING-FIELD-NUMBER.
000574     MOVE "PLAYER 1" TO PLAYER-NAME.
000575     PERFORM ASK-PLAYER-MOVE.
000576     PERFORM DO-PLAYER-MOVE.
000577     PERFORM CHECK-FOR-WIN.
000578     IF GAME-OVER NOT = "Y" AND MOVES-MADE NOT >= 9
000579         MOVE 1 TO PLAYING-FIELD-NUMBER
000580         IF AMOUNT-OF-PLAYERS = 1
000581             MOVE "CPU" TO PLAYER-NAME
000582             PERFORM DO-COMP-MOVE
000583             PERFORM CHECK-FOR-WIN
000584         ELSE
000585             MOVE "PLAYER 2" TO PLAYER-NAME
000586             PERFORM ASK-PLAYER-MOVE
000587             PERFORM DO-PLAYER-MOVE
000588             PERFORM CHECK-FOR-WIN.
000589     
000590* -----------------------------------------------------
000591*        PLAYER ROUTINES
000592* -----------------------------------------------------
000593*
000594 ASK-PLAYER-MOVE.
000595     MOVE "N" TO PLAYER-MOVE-VALID.
000596     PERFORM ACCEPT-PLAYER-MOVE.
000597     PERFORM CHECK-PLAYER-MOVE
000598         UNTIL PLAYER-MOVE-VALID = "Y".
000599
000600 ACCEPT-PLAYER-MOVE.
000601     MOVE SPACES TO INPUT-PLAYER-MOVE.
000602     PERFORM DISPLAY-PLAYING-FIELD.
000603     ACCEPT PLAYING-FIELD.
000604     MOVE FUNCTION TRIM(INPUT-PLAYER-MOVE) TO PLAYER-MOVE.
000605     MOVE SPACES TO ERROR-MESSAGE.
000606 
000607 CHECK-PLAYER-MOVE.
000608     MOVE "Y" TO PLAYER-MOVE-VALID.
000609* IN COBOL ARRAYS START AT 1
000610* SO YOU HAVE TO MAKE SURE YOU DON'T ASK FOR THE 0TH PLACE.
000611     IF PLAYER-MOVE-NUM
000612         IF FIELD-SQUARE(PLAYER-MOVE) NOT = 0
000613             MOVE "THIS FIELD IS ALREADY TAKEN" TO ERROR-MESSAGE
000614             MOVE "N" TO PLAYER-MOVE-VALID
000615             PERFORM ACCEPT-PLAYER-MOVE
000616         END-IF
000617     ELSE 
000618         MOVE "NOT A VALID ENTRY" TO ERROR-MESSAGE
000619         MOVE "N" TO PLAYER-MOVE-VALID
000620         PERFORM ACCEPT-PLAYER-MOVE.
000621
000622 DO-PLAYER-MOVE.
000623     MOVE PLAYING-FIELD-NUMBER TO FIELD-SQUARE(PLAYER-MOVE).
000624     ADD 1 TO MOVES-MADE.
000625
000626* -----------------------------------------------------
000627*        CHECK FOR WIN ROUTINES
000628* -----------------------------------------------------
000629 CHECK-FOR-WIN.
000630     PERFORM CHECK-WIN-ROWS.
000631     PERFORM CHECK-WIN-COLUMNS.
000632     PERFORM CHECK-WIN-DIAGONALS.
000633
000634 CHECK-WIN-ROWS.
000635     PERFORM CHECK-WIN-SINGEL-ROW
000636         VARYING CHECK-INDEX FROM 0 BY 3
000637             UNTIL CHECK-INDEX > 6
000638                OR GAME-OVER = "Y".
000639
000640 CHECK-WIN-SINGEL-ROW.
000641     COMPUTE FIELD-CALCULATION = 
000642                               FIELD-SQUARE(CHECK-INDEX + 1) +
000643                               FIELD-SQUARE(CHECK-INDEX + 2) +
000644                               FIELD-SQUARE(CHECK-INDEX + 3).
000645
000646     IF FIELD-CALCULATION = 3
000647         PERFORM PLAYER-2-WON
000648         MOVE WIN-COLOR TO FIELD-DISPLAY-COLOR(CHECK-INDEX + 1)
000649         MOVE WIN-COLOR TO FIELD-DISPLAY-COLOR(CHECK-INDEX + 2)
000650         MOVE WIN-COLOR TO FIELD-DISPLAY-COLOR(CHECK-INDEX + 3).
000651
000652
000653     IF FIELD-CALCULATION = 21
000654         PERFORM PLAYER-1-WON
000655         MOVE WIN-COLOR TO FIELD-DISPLAY-COLOR(CHECK-INDEX + 1)
000656         MOVE WIN-COLOR TO FIELD-DISPLAY-COLOR(CHECK-INDEX + 2)
000657         MOVE WIN-COLOR TO FIELD-DISPLAY-COLOR(CHECK-INDEX + 3).
000658 
000659 CHECK-WIN-COLUMNS.
000660     PERFORM CHECK-WIN-SINGEL-COLUMN
000661         VARYING CHECK-INDEX FROM 0 BY 1
000662             UNTIL CHECK-INDEX > 3
000663                OR GAME-OVER = "Y".
000664
000665 CHECK-WIN-SINGEL-COLUMN.
000666     COMPUTE FIELD-CALCULATION = 
000667                               FIELD-SQUARE(CHECK-INDEX + 1) +
000668                               FIELD-SQUARE(CHECK-INDEX + 4) +
000669                               FIELD-SQUARE(CHECK-INDEX + 7).
000670
000671     IF FIELD-CALCULATION = 3
000672         PERFORM PLAYER-2-WON
000673         MOVE WIN-COLOR TO FIELD-DISPLAY-COLOR(CHECK-INDEX + 1)
000674         MOVE WIN-COLOR TO FIELD-DISPLAY-COLOR(CHECK-INDEX + 4)
000675         MOVE WIN-COLOR TO FIELD-DISPLAY-COLOR(CHECK-INDEX + 7).
000676
000677     IF FIELD-CALCULATION = 21
000678         PERFORM PLAYER-1-WON
000679         MOVE WIN-COLOR TO FIELD-DISPLAY-COLOR(CHECK-INDEX + 1)
000680         MOVE WIN-COLOR TO FIELD-DISPLAY-COLOR(CHECK-INDEX + 4)
000681         MOVE WIN-COLOR TO FIELD-DISPLAY-COLOR(CHECK-INDEX + 7).
000682
000683
000684 CHECK-WIN-DIAGONALS.
000685     PERFORM CHECK-WIN-LEFT-RIGHT-DIAG.
000686     PERFORM CHECK-WIN-RIGHT-LEFT-DIAG.
000687 
000688 CHECK-WIN-RIGHT-LEFT-DIAG.
000689     COMPUTE FIELD-CALCULATION = 
000690                               FIELD-SQUARE(1) +
000691                               FIELD-SQUARE(5) +
000692                               FIELD-SQUARE(9).
000693
000694     IF FIELD-CALCULATION = 3
000695         PERFORM PLAYER-2-WON
000696         MOVE WIN-COLOR TO FIELD-DISPLAY-COLOR(1)
000697         MOVE WIN-COLOR TO FIELD-DISPLAY-COLOR(5)
000698         MOVE WIN-COLOR TO FIELD-DISPLAY-COLOR(9).
000699
000700     IF FIELD-CALCULATION = 21
000701         PERFORM PLAYER-1-WON
000702         MOVE WIN-COLOR TO FIELD-DISPLAY-COLOR(1)
000703         MOVE WIN-COLOR TO FIELD-DISPLAY-COLOR(5)
000704         MOVE WIN-COLOR TO FIELD-DISPLAY-COLOR(9).
000705 
000706 CHECK-WIN-LEFT-RIGHT-DIAG.
000707     COMPUTE FIELD-CALCULATION = 
000708                               FIELD-SQUARE(3) +
000709                               FIELD-SQUARE(5) +
000710                               FIELD-SQUARE(7).
000711
000712     IF FIELD-CALCULATION = 3
000713         PERFORM PLAYER-2-WON
000714         MOVE WIN-COLOR TO FIELD-DISPLAY-COLOR(3)
000715         MOVE WIN-COLOR TO FIELD-DISPLAY-COLOR(5)
000716         MOVE WIN-COLOR TO FIELD-DISPLAY-COLOR(7).
000717
000718     IF FIELD-CALCULATION = 21
000719         PERFORM PLAYER-1-WON
000720         MOVE WIN-COLOR TO FIELD-DISPLAY-COLOR(3)
000721         MOVE WIN-COLOR TO FIELD-DISPLAY-COLOR(5)
000722         MOVE WIN-COLOR TO FIELD-DISPLAY-COLOR(7).
000723
000724 PLAYER-1-WON.
000725     MOVE "PLAYER 1 WON!" TO END-MESSAGE.
000726     MOVE GREEN TO END-MESSAGE-COLOR
000727                   WIN-COLOR.
000728     MOVE "Y" TO GAME-OVER.
000729
000730 PLAYER-2-WON.
000731     IF AMOUNT-OF-PLAYERS = 1
000732         MOVE "   YOU LOST!" TO END-MESSAGE
000733         MOVE RED TO END-MESSAGE-COLOR
000734                     WIN-COLOR
000735     ELSE
000736         MOVE "PLAYER 2 WON!" TO END-MESSAGE
000737         MOVE GREEN TO END-MESSAGE-COLOR
000738                       WIN-COLOR.
000739
000740     MOVE "Y" TO GAME-OVER.
000741
000742* -----------------------------------------------------
000743*        COMPUTER MOVES
000744* -----------------------------------------------------
000745* CHECK-SUM IS WHAT THE CHECKS SHOULD LOOK FOR 
000746* 2 ARE ROWS WHERE THE COMPUTER HAS TWO FIELDS AND ONE EMPTY ONE.
000747* IN ORDER TO WIN THE COMP NEEDS TO TAKE THE EMPTY ONE THERE.
000748* 14 ARE PLACES WHERE THE PLAYER HAS TWO FIELDS AND AN EMPTY ONE.
000749* TO MAKE SURE PLAYR DOESN'T WIN, COMP MUST TAKE THE EMPTY FIELD
000750* THERE.
000751 DO-COMP-MOVE.
000752     MOVE "N" TO COMP-MOVE-MADE.
000753
000754     MOVE 2 TO CHECK-SUM.
000755     PERFORM CHECK-COMP-WIN.
000756
000757*    CHECK IF THE PLAYER COULD WIN
000758     IF COMP-MOVE-MADE = "N" AND COMP-LEVEL > 1
000759         MOVE 14 TO CHECK-SUM
000760         PERFORM CHECK-COMP-WIN.
000761     
000762     IF COMP-MOVE-MADE = "N" AND COMP-LEVEL > 2
000763         PERFORM COMP-CLAIM-MIDDLE.
000764
000765*    ONLY NEEDS TO BE DONE IF COMP HAS THE MIDDLE FIELD.
000766     IF COMP-MOVE-MADE = "N"
000767       AND FIELD-SQUARE(5) = 1
000768       AND COMP-LEVEL > 3
000769         PERFORM COMP-CHECK-MIDDLE-SIDE.
000770
000771     IF COMP-MOVE-MADE = "N"
000772       AND FIELD-SQUARE(5) = 1
000773       AND COMP-LEVEL > 4
000774         PERFORM COMP-CLAIM-SIDE.
000775
000776     IF COMP-MOVE-MADE = "N"
000777       AND FIELD-SQUARE(5) = 7
000778       AND COMP-LEVEL > 5
000779         PERFORM COMP-CLAIM-CORNER.
000780     
000781     IF COMP-MOVE-MADE = "N"
000782         PERFORM COMP-TAKE-RANDOM.
000783
000784 
000785 CHECK-COMP-WIN.
000786     PERFORM COMP-WIN-CHECK-ROWS.
000787     PERFORM COMP-WIN-CHECK-COLS.
000788     PERFORM COMP-WIN-CHECK-DIAGS.
000789
000790* -----------------------------------------------------
000791*        CHECK THE ROWS FOR A POSSIBLE WIN
000792* -----------------------------------------------------
000793 COMP-WIN-CHECK-ROWS.
000794     PERFORM COMP-WIN-CHECK-ROW
000795         VARYING CHECK-INDEX FROM 0 BY 3
000796             UNTIL CHECK-INDEX > 6
000797                OR COMP-MOVE-MADE = "Y".
000798
000799 COMP-WIN-CHECK-ROW.
000800      COMPUTE FIELD-CALCULATION = 
000801                               FIELD-SQUARE(CHECK-INDEX + 1) +
000802                               FIELD-SQUARE(CHECK-INDEX + 2) +
000803                               FIELD-SQUARE(CHECK-INDEX + 3).
000804
000805* IF THE RESAULT IS 2, THE COMPUTER HAS TWO FIELDS OCCUPIED
000806* BY HIS OWN THERE. TO WIN HE'D HAVE TO PLACE HIS PICE THERE.
000807     IF FIELD-CALCULATION = CHECK-SUM
000808         PERFORM COMP-PLACE-ROW.
000809
000810* FIND THE EMPTY FIELD IN THAT ROW AND PLACE IT'S PIEC THERE.
000811 COMP-PLACE-ROW.
000812     IF FIELD-SQUARE(CHECK-INDEX + 1) = 0
000813         MOVE 1 TO FIELD-SQUARE(CHECK-INDEX + 1)
000814     ELSE 
000815     IF FIELD-SQUARE(CHECK-INDEX + 2) = 0
000816         MOVE 1 TO FIELD-SQUARE(CHECK-INDEX + 2)
000817     ELSE
000818     IF FIELD-SQUARE(CHECK-INDEX + 3) = 0
000819         MOVE 1 TO FIELD-SQUARE(CHECK-INDEX + 3).
000820     
000821     PERFORM COMP-FINISH-MOVE.
000822
000823* -----------------------------------------------------
000824*        CHECK THE COLUMNS FOR A POSSIBLE WIN
000825* -----------------------------------------------------
000826 COMP-WIN-CHECK-COLS.
000827     PERFORM COMP-WIN-CHECK-COL
000828         VARYING CHECK-INDEX FROM 0 BY 1
000829             UNTIL CHECK-INDEX > 3
000830                OR COMP-MOVE-MADE = "Y".
000831
000832 COMP-WIN-CHECK-COL.
000833     COMPUTE FIELD-CALCULATION = 
000834                               FIELD-SQUARE(CHECK-INDEX + 1) +
000835                               FIELD-SQUARE(CHECK-INDEX + 4) +
000836                               FIELD-SQUARE(CHECK-INDEX + 7).
000837     
000838     IF FIELD-CALCULATION = CHECK-SUM
000839         PERFORM COMP-PLACE-COL.
000840
000841 COMP-PLACE-COL.
000842     IF FIELD-SQUARE(CHECK-INDEX + 1) = 0
000843         MOVE 1 TO FIELD-SQUARE(CHECK-INDEX + 1)
000844     ELSE 
000845     IF FIELD-SQUARE(CHECK-INDEX + 4) = 0
000846         MOVE 1 TO FIELD-SQUARE(CHECK-INDEX + 4)
000847     ELSE
000848     IF FIELD-SQUARE(CHECK-INDEX + 7) = 0
000849         MOVE 1 TO FIELD-SQUARE(CHECK-INDEX + 7).
000850     
000851     PERFORM COMP-FINISH-MOVE.
000852
000853* -----------------------------------------------------
000854*        CHECK THE DIAGONALS FOR A POSSIBLE WIN
000855* -----------------------------------------------------
000856 COMP-WIN-CHECK-DIAGS.
000857     IF COMP-MOVE-MADE = "N"
000858         PERFORM COMP-WIN-CHECK-DIAG-L.
000859     
000860     IF COMP-MOVE-MADE = "N"
000861         PERFORM COMP-WIN-CHECK-DIAG-R.
000862
000863
000864 COMP-WIN-CHECK-DIAG-L.
000865     COMPUTE FIELD-CALCULATION = 
000866                               FIELD-SQUARE(1) +
000867                               FIELD-SQUARE(5) +
000868                               FIELD-SQUARE(9).
000869     
000870     IF FIELD-CALCULATION = CHECK-SUM
000871         PERFORM COMP-PLACE-DIAG-L.
000872
000873 COMP-PLACE-DIAG-L.
000874     IF FIELD-SQUARE(1) = 0
000875         MOVE 1 TO FIELD-SQUARE(1)
000876     ELSE 
000877     IF FIELD-SQUARE(5) = 0
000878         MOVE 1 TO FIELD-SQUARE(5)
000879     ELSE
000880     IF FIELD-SQUARE(9) = 0
000881         MOVE 1 TO FIELD-SQUARE(9).
000882     
000883     PERFORM COMP-FINISH-MOVE.
000884 
000885 COMP-WIN-CHECK-DIAG-R.
000886     COMPUTE FIELD-CALCULATION = 
000887                               FIELD-SQUARE(3) +
000888                               FIELD-SQUARE(5) +
000889                               FIELD-SQUARE(7).
000890     
000891     IF FIELD-CALCULATION = CHECK-SUM
000892         PERFORM COMP-PLACE-DIAG-R.
000893
000894 COMP-PLACE-DIAG-R.
000895     IF FIELD-SQUARE(3) = 0
000896         MOVE 1 TO FIELD-SQUARE(3)
000897     ELSE 
000898     IF FIELD-SQUARE(5) = 0
000899         MOVE 1 TO FIELD-SQUARE(5)
000900     ELSE
000901     IF FIELD-SQUARE(7) = 0
000902         MOVE 1 TO FIELD-SQUARE(7).
000903     
000904     PERFORM COMP-FINISH-MOVE.
000905 
000906* -----------------------------------------------------
000907*        OTHER CHECK ROUTINES
000908*  DO THESE WHEN THERE ARE NO WINNING OR COUNTER WINNING
000909*  MOVES.
000910* -----------------------------------------------------
000911* IF POSSIBLE, CLAIM THE MIDDLE.
000912 COMP-CLAIM-MIDDLE.
000913     IF FIELD-SQUARE(5) = 0
000914         MOVE 1 TO FIELD-SQUARE(5)
000915         PERFORM COMP-FINISH-MOVE.
000916 
000917* IF COMP HAS THE MIDDLE AND THE PLAYER HAS A SIDE TAKE A CORNER
000918* NEXT TO THAT SIDE. SINCE THIS PARAGRAPH ONLY GETS CALLED IF
000919* THE COMP HAS THE MIDDLE ALREADY, I WON'T CHECK FOR THE MIDDLE.
000920 COMP-CHECK-MIDDLE-SIDE.
000921     IF COMP-MOVE-MADE = "N" AND FIELD-SQUARE(2) = 7
000922         PERFORM CHECK-MIDDLE-SIDE-2.
000923
000924     IF COMP-MOVE-MADE = "N" AND FIELD-SQUARE(4) = 7
000925         PERFORM CHECK-MIDDLE-SIDE-4.
000926
000927     IF COMP-MOVE-MADE = "N" AND FIELD-SQUARE(6) = 7
000928         PERFORM CHECK-MIDDLE-SIDE-6.
000929
000930     IF COMP-MOVE-MADE = "N" AND FIELD-SQUARE(8) = 7
000931         PERFORM CHECK-MIDDLE-SIDE-8.
000932
000933 CHECK-MIDDLE-SIDE-2.
000934     IF FIELD-SQUARE(1) = 0
000935         MOVE 1 TO FIELD-SQUARE(1)
000936         PERFORM COMP-FINISH-MOVE
000937     ELSE 
000938     IF FIELD-SQUARE(3) = 0
000939         MOVE 1 TO FIELD-SQUARE(3)
000940         PERFORM COMP-FINISH-MOVE.
000941
000942 CHECK-MIDDLE-SIDE-4.
000943     IF FIELD-SQUARE(1) = 0
000944         MOVE 1 TO FIELD-SQUARE(1)
000945         PERFORM COMP-FINISH-MOVE
000946     ELSE 
000947     IF FIELD-SQUARE(7) = 0
000948         MOVE 1 TO FIELD-SQUARE(7)
000949         PERFORM COMP-FINISH-MOVE.
000950
000951 CHECK-MIDDLE-SIDE-6.
000952     IF FIELD-SQUARE(3) = 0
000953         MOVE 1 TO FIELD-SQUARE(3)
000954         PERFORM COMP-FINISH-MOVE
000955     ELSE 
000956     IF FIELD-SQUARE(9) = 0
000957         MOVE 1 TO FIELD-SQUARE(9)
000958         PERFORM COMP-FINISH-MOVE.
000959
000960 CHECK-MIDDLE-SIDE-8.
000961     IF FIELD-SQUARE(7) = 0
000962         MOVE 1 TO FIELD-SQUARE(7)
000963         PERFORM COMP-FINISH-MOVE
000964     ELSE 
000965     IF FIELD-SQUARE(9) = 0
000966         MOVE 1 TO FIELD-SQUARE(9)
000967         PERFORM COMP-FINISH-MOVE.
000968 
000969* IF THE COMPUTER HAS THE MIDDLE (GETS CHECKED BEFORE THIS
000970* PARAGRAPH GETS CALLED) CLAIM A SIDE.
000971 COMP-CLAIM-SIDE.
000972     IF FIELD-SQUARE(2) = 0
000973         MOVE 1 TO FIELD-SQUARE(2)
000974         PERFORM COMP-FINISH-MOVE
000975     ELSE 
000976     IF FIELD-SQUARE(4) = 0
000977         MOVE 1 TO FIELD-SQUARE(4)
000978         PERFORM COMP-FINISH-MOVE
000979     ELSE
000980     IF FIELD-SQUARE(6) = 0
000981         MOVE 1 TO FIELD-SQUARE(6)
000982         PERFORM COMP-FINISH-MOVE
000983     ELSE
000984     IF FIELD-SQUARE(8) = 0
000985         MOVE 1 TO FIELD-SQUARE(8)
000986         PERFORM COMP-FINISH-MOVE.
000987
000988* IF THE PLAYER HAS THE MIDDLE (GETS CHECKED BEFORE THIS
000989* PARAGRAPH GETS CALLED) CLAIM A CORNER.
000990 COMP-CLAIM-CORNER.
000991     IF FIELD-SQUARE(1) = 0
000992         MOVE 1 TO FIELD-SQUARE(1)
000993         PERFORM COMP-FINISH-MOVE
000994     ELSE 
000995     IF FIELD-SQUARE(3) = 0
000996         MOVE 1 TO FIELD-SQUARE(3)
000997         PERFORM COMP-FINISH-MOVE
000998     ELSE
000999     IF FIELD-SQUARE(7) = 0
001000         MOVE 1 TO FIELD-SQUARE(7)
001001         PERFORM COMP-FINISH-MOVE
001002     ELSE
001003     IF FIELD-SQUARE(9) = 0
001004         MOVE 1 TO FIELD-SQUARE(9)
001005         PERFORM COMP-FINISH-MOVE.
001006
001007* IF ALL OTHER CHECKS FAIL, TAKE THE NEXT FREE SPACE.
001008 COMP-TAKE-RANDOM.
001009     PERFORM RANDOM-SPACE
001010         VARYING CHECK-INDEX FROM 1 BY 1
001011         UNTIL CHECK-INDEX > 9
001012            OR COMP-MOVE-MADE = "Y".
001013
001014 RANDOM-SPACE.
001015     IF FIELD-SQUARE(CHECK-INDEX) = 0
001016         MOVE 1 TO FIELD-SQUARE(CHECK-INDEX)
001017         PERFORM COMP-FINISH-MOVE.
001018 
001019
001020 COMP-FINISH-MOVE.
001021     MOVE "Y" TO COMP-MOVE-MADE.
001022     ADD 1 TO MOVES-MADE.
001023
001024 DRAW-FIELDS.
001025     PERFORM DRAW-FIELD
001026          VARYING CHECK-INDEX FROM 1 BY 1
001027             UNTIL CHECK-INDEX > 9.
001028
001029 DRAW-FIELD.
001030     IF FIELD-SQUARE(CHECK-INDEX) = 1
001031          MOVE O-PIECE TO FIELD-SQUARE-DISPLAY(CHECK-INDEX)
001032     ELSE
001033     IF FIELD-SQUARE(CHECK-INDEX) = 7
001034          MOVE X-PIECE TO FIELD-SQUARE-DISPLAY(CHECK-INDEX).
001035 
001036 DISPLAY-PLAYING-FIELD.
001037     PERFORM DRAW-FIELDS.
001038     PERFORM COLOR-THE-FIELDS.
001039     DISPLAY PLAYING-FIELD.
001040
001041 COLOR-THE-FIELDS.
001042     MOVE FIELD-DISPLAY-COLOR(1) TO COLOR-1.
001043     MOVE FIELD-DISPLAY-COLOR(2) TO COLOR-2.
001044     MOVE FIELD-DISPLAY-COLOR(3) TO COLOR-3.
001045     MOVE FIELD-DISPLAY-COLOR(4) TO COLOR-4.
001046     MOVE FIELD-DISPLAY-COLOR(5) TO COLOR-5.
001047     MOVE FIELD-DISPLAY-COLOR(6) TO COLOR-6.
001048     MOVE FIELD-DISPLAY-COLOR(7) TO COLOR-7.
001049     MOVE FIELD-DISPLAY-COLOR(8) TO COLOR-8.
001050     MOVE FIELD-DISPLAY-COLOR(9) TO COLOR-9.
001051
001052 END-SCREEN.
001053     PERFORM DISPLAY-PLAYING-FIELD.
001054     DISPLAY END-SCREEN-SCREEN.
001055     ACCEPT END-SCREEN-SCREEN.
001056
001057*    INSPECT PLAY-AGAIN
001058*        CONVERTING LOWER-ALPHA TO UPPER-ALPHA.
001059     MOVE FUNCTION TRIM(INPUT-PLAY-AGAIN) TO PLAY-AGAIN.
001060
001061*    MOVE SPACES TO DEBUG-TEXT.
001062*    MOVE PLAY-AGAIN TO DEBUG-TEXT.
001063*    DISPLAY DEBUG.
001064*    ACCEPT A-DUMMY.
001065
001066     IF PLAY-AGAIN-YES
001067         PERFORM MAIN-PROCESS.
001068
001069
001070
001071
001072
001073
001074
001075
001076
